# Copyright 2023 Google LLC.

import("../../gn/skia.gni")
import("bentleyottmann.gni")

declare_args() {
  skia_enable_bentleyottmann = true
}

if (skia_enable_bentleyottmann) {
  config("public_config") {
    include_dirs = [ "include" ]
  }

  skia_component("bentleyottmann") {
    import("bentleyottmann.gni")
    public_configs = [ ":public_config" ]
    public = bentleyottmann_public
    sources = bentleyottmann_sources
    deps = [ "../..:skia" ]
  }

  if (skia_enable_tools) {
    skia_source_set("tests") {
      testonly = true
      sources = bentleyottmann_tests
      deps = [
        ":bentleyottmann",
        "../..:skia",
        "../..:test",
      ]
      if (is_ohos) {
        external_deps = [ "hitrace:hitrace_meter" ]
      }
      if (is_arkui_x) {
          deps += [ "//third_party/bounds_checking_function:libsec_static" ]
      } else if (is_ohos) {
        if (is_component_build) {
          external_deps += [ "bounds_checking_function:libsec_shared" ]
        } else {
          external_deps += [ "bounds_checking_function:libsec_static" ]
        }
      }
    }
  }
}
